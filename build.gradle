group 'com.rhtech'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath(
                'org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE'
        )
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/intTest/java')
        }
        resources.srcDirs = ['src/intTest/resources', 'src/main/resources']
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.rhtech.servisim.Application'
    }
}

configurations {
    intTestCompile.extendsFrom testCompile
    intTestRuntime.extendsFrom testRuntime
}

task intTest(type: Test) {
    testClassesDir = sourceSets.intTest.output.classesDir
    classpath = sourceSets.intTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn intTest
intTest.mustRunAfter test

dependencies {

    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }

    compile('org.springframework.boot:spring-boot-starter-undertow')

    compile('org.springframework.boot:spring-boot-starter-data-jpa')

    compile('mysql:mysql-connector-java:6.0.6')

    /*compile("org.springframework.boot:spring-boot-starter-actuator")*/

    compileOnly "org.projectlombok:lombok:1.16.16"

    testCompile("org.springframework.boot:spring-boot-starter-test")

    testCompile "com.h2database:h2:1.4+"

}
